plugins {
    id "com.timgroup.jarmangit" version "1.1.117" apply false
    id "com.github.maiflai.scalatest" version "0.25" apply false
    id "com.google.protobuf" version "0.8.11" apply false
    id "scala-properties"
}

ext {
    buildNumber = System.getenv("BUILD_NUMBER") ?: System.getenv("TRAVIS_BUILD_NUMBER")
    mysqlJdbcVersion = "5.1.47"
}

tasks.register("createDb") {
    doFirst {
        def mysql_exec = { db, cmd ->
            project.exec {
                if (db) {
                    commandLine("mysql", db, "-e$cmd")
                }
                else {
                    commandLine("mysql", "-e$cmd")
                }
                environment("MYSQL_HOST", "127.0.0.1")
            }
        }
        mysql_exec(null, "drop database if exists sql_eventstore")
        mysql_exec(null, "create database sql_eventstore")
        mysql_exec("sql_eventstore", "CREATE TABLE Event(eventType VARCHAR(255), body BLOB, version INT PRIMARY KEY, effective_timestamp datetime)")
    }
}

tasks.register("publishScala")

subprojects {
    afterEvaluate {
        if (project.plugins.hasPlugin("scala")) {
            rootProject.tasks.named("publishScala").configure {
                it.dependsOn(tasks.named("publish"))
            }
        }
    }
}
