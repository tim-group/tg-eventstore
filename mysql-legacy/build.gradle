plugins {
    id "java"
    id "scala"
    id "com.timgroup.jarmangit"
    id "com.github.maiflai.scalatest"
    id "maven-publish"
}

group = 'com.timgroup'
if (buildNumber) version = '0.0.' + buildNumber

repositories {
    maven {
        url = "$repoUrl/groups/public"
    }
}

// https://chrismarks.wordpress.com/2013/07/31/compiling-a-mixed-scala-project-with-gradle/
sourceSets.main.scala.srcDir "src/main/java"
sourceSets.main.java.srcDirs = []
sourceSets.test.scala.srcDir "src/test/java"
sourceSets.test.java.srcDirs = []

dependencies {
    compile project(":api-legacy")
    compile project(":mysql")
    compile "com.fasterxml.jackson.core:jackson-databind:2.7.2"
    compileOnly "org.joda:joda-convert:1.3.1"

    testCompile project(":api-legacy:testlib")
    testCompile project(":memory-legacy")
    testCompile "junit:junit:4.12"
    testCompile "org.hamcrest:hamcrest-core:1.3"
    testCompile "org.hamcrest:hamcrest-library:1.3"
    testCompile "com.timgroup:clocks-testing:1.0.1087" // autobump
    testCompile "org.scalatest:scalatest_$scalaApiVersion:3.0.1"
    testCompileOnly "org.joda:joda-convert:1.3.1"
    testRuntime "org.pegdown:pegdown:1.4.2"
    testCompile "mysql:mysql-connector-java:8.0.13"
}

check.dependsOn("scalatest")

tasks.withType(JavaCompile).all {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
    options.encoding = "UTF-8"
    options.incremental = true
    options.deprecation = true
    options.compilerArgs << "-parameters"
}

tasks.withType(ScalaCompile).all {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
    options.encoding = "UTF-8"
    options.incremental = true
    options.deprecation = true
    options.compilerArgs << "-parameters"
    scalaCompileOptions.additionalParameters = ["-target:jvm-1.8"]
}

tasks.withType(Jar).all {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'TIM Group Ltd'
        )
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

assemble.dependsOn(sourcesJar)

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

assemble.dependsOn(javadocJar)

scalatest {
    maxParallelForks = 1
    dependsOn(":createDb")
}

publishing {
    repositories {
        if (project.hasProperty("repoUrl")) {
            maven {
                url = "${project.repoUrl}/repositories/yd-release-candidates"
                credentials {
                    username = project.repoUsername
                    password = project.repoPassword
                }
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId "eventstore-mysql-legacy_${scalaApiVersion}"
            from components.java
            artifact(sourcesJar)
            artifact(javadocJar)
        }
    }
}
