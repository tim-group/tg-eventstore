plugins {
    id "java-library"
    id "com.timgroup.jarmangit"
    id "maven-publish"
}

group = 'com.timgroup'
if (buildNumber) version = '0.0.' + buildNumber

repositories {
    maven {
        name "nexus"
        url "${repoUrl}/groups/public"
    }
}

dependencies {
    api project(":api")
    api "com.typesafe:config:1.2.1"
    api "com.mchange:c3p0:0.9.5.2"
    compileOnly "com.google.code.findbugs:jsr305:1.3.9"
    implementation "com.timgroup:tim-logger:1.5.1094" // autobump
    implementation "io.dropwizard.metrics:metrics-core:3.2.3"

    testCompile project(":api:testlib")
    testCompile project(":memory")
    testCompile "junit:junit:4.12"
    testCompile "org.hamcrest:hamcrest-core:1.3"
    testCompile "org.hamcrest:hamcrest-library:1.3"
    testCompile "com.timgroup:clocks-testing:1.0.1087" // autobump
    testCompile "mysql:mysql-connector-java:8.0.13"
    testCompileOnly "com.google.code.findbugs:jsr305:1.3.9"
}

tasks.withType(JavaCompile).all {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
    options.encoding = "UTF-8"
    options.incremental = true
    options.deprecation = true
    options.compilerArgs << "-parameters"
}

tasks.withType(Jar).all {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'TIM Group Ltd'
        )
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

assemble.dependsOn(sourcesJar)

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

assemble.dependsOn(javadocJar)

test.dependsOn(":createDb")

publishing {
    repositories {
        if (project.hasProperty("repoUrl")) {
            maven {
                name "nexus"
                url "${project.repoUrl}/repositories/yd-release-candidates"
                credentials {
                    username = project.repoUsername
                    password = project.repoPassword
                }
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId "eventstore-mysql"
            from components.java
            artifact(sourcesJar)
            artifact(javadocJar)
        }
    }
}
