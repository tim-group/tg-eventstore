plugins {
    id "java-library"
    id "idea"
    id "com.google.protobuf"
    id "com.timgroup.jarmangit"
    id "maven-publish"
}

group = 'com.timgroup'
if (buildNumber) version = "0.0.${buildNumber}"

repositories {
    maven {
        url = "$repoUrl/groups/public"
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    api project(":api")
    api project(":mysql") 
    api project(":subscription")
    api "com.google.protobuf:protobuf-java:$protobufVersion"
    api "com.timgroup:Tucker:1.0.1584" // autobump
    api "com.timgroup:tim-structured-events:0.4.1288" // autobump

    implementation "com.google.code.findbugs:jsr305:3.0.2"
    implementation "com.timgroup:tim-logger:1.5.1109" // autobump
    implementation "com.timgroup:tim-remote-file-storage-s3:0.0.1227" // autobump

    runtimeOnly "javax.xml.bind:jaxb-api:2.3.0"
    runtimeOnly "com.sun.xml.bind:jaxb-core:2.3.0"
    runtimeOnly "com.sun.xml.bind:jaxb-impl:2.3.0"
    runtimeOnly "com.sun.xml.ws:jaxws-rt:2.3.2"
    runtimeOnly "javax.annotation:javax.annotation-api:1.3.1"
    
    testImplementation project(":api:testlib")
    testImplementation project(":memory")

    testImplementation "junit:junit:4.13.2"
    testImplementation "org.hamcrest:hamcrest-core:1.3"
    testImplementation "org.hamcrest:hamcrest-library:1.3"

    testImplementation "com.timgroup:tg-config-java-bindings:1.0.86" // autobump
    testImplementation "org.apache.commons:commons-lang3:3.3.2"
    testImplementation "com.timgroup:clocks-testing:1.0.1111" // autobump
    testImplementation "net.ttsui.junit.rules.pending:pending:0.0.1055"
    testImplementation "org.mockito:mockito-core:3.7.7"
    testImplementation "com.youdevise:Matchers:0.0.1293" // autobump
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.incremental = true
    options.deprecation = true
    options.compilerArgs << "-parameters"
    options.compilerArgs << "-Xlint:unchecked"
}

tasks.withType(Jar) {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'TIM Group Ltd'
        )
    }
}

tasks.named("jar").configure {
    manifest {
        attributes(
                'Automatic-Module-Name': "com.timgroup.eventstore.archiver"
        )
    }
}

publishing {
    repositories {
        if (project.hasProperty("repoUrl")) {
            maven {
                url "${project.repoUrl}/repositories/yd-release-candidates"
                credentials {
                    username = project.repoUsername
                    password = project.repoPassword
                }
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId "eventstore-archiver"
            from components.java
        }
    }
}
